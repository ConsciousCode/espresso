#*
 * Standard Brainfuck interpreter with hello world
 *
 * Tests:
 *  - Lists and list literals
 *  - Indexing
 *  - Unary ++ and --
 *  - Method calls
 *  - Switch-case
 *#
var src = "--[+++++++<---->>-->+>+>+<<<<]<.>++++[-<++++>>->--<<]>>-.>--..>+.<<<.<<-.>>+>->>.+++[.<]";

var pc = 0, dp = 0, stack = [], data = [0];

while(pc < src.length) {
	#print(src[pc], pc, dp, stack, data, '\n');
	switch(src[pc++]) {
		case "<" => {
			if(dp == 0) {
				data.push_front(0);
			}
			else --dp;
		}
		case ">" => {
			if(++dp >= data.length) {
				data.push(0);
			}
		}
		case "+" => ++data[dp];
		case '-' => --data[dp];
		case '.' => print(char(data[dp]));
		case "[" => stack.push(pc);
		case "]" => {
			if(data[dp]) pc = stack[-1];
			else stack.pop();
		}
	}
	
	data[dp] &= 0xff;
}
